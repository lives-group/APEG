# Hierarchical syntax
Grammar    <- Spacing Definition+ EndOfFile                     # Type 0
Definition <- Identifier LEFTARROW Expression                   # Type 1

Expression <- Sequence (SLASH Sequence)*                        # Type 2
Sequence   <- Prefix*                                           # Type 3
Prefix     <- (AND / NOT)? Suffix                               # Type 4
Suffix     <- Primary (QUESTION / STAR / PLUS)?                 # Type 5
Primary    <- Identifier !LEFTARROW                             # Type 6
            / OPEN Expression CLOSE
            / Literal / DOT

# Lexical syntax
Identifier <- '[a-zA-Z_][a-zA-Z_0-9]*' Spacing                  # Type 7

Literal    <- "'" (!"'" Char)* "'" Spacing                      # Type 8
            / '"' (!'"' Char)* '"' Spacing
Char       <- '\\[nrt\'"\+\-\*\.\?\^\$\{\}\(\)\[\]\\]'          # Type 9
            / '\\[0-2][0-7][0-7]'
            / '\\[0-7][0-7]?'
            / !'\\' .

LEFTARROW  <- '<-' Spacing                                      # Type 10
SLASH      <- '/' Spacing                                       # Type 11
AND        <- '&' Spacing                                       # Type 12
NOT        <- '!' Spacing                                       # Type 13
QUESTION   <- '\\?' Spacing                                     # Type 14
STAR       <- '\\*' Spacing                                     # Type 15
PLUS       <- '\\+' Spacing                                     # Type 16
OPEN       <- '\\(' Spacing                                     # Type 17
CLOSE      <- '\\)' Spacing                                     # Type 18
DOT        <- '\\.' Spacing                                     # Type 19

Spacing    <- (Space / Comment)*                                # Type 20
Comment    <- '#' (!EndOfLine .)* EndOfLine                     # Type 21
Space      <- ' ' / '\t' / EndOfLine                            # Type 22
EndOfLine  <- '\r\n' / '\n' / '\r'                              # Type 23
EndOfFile  <- !.                                                # Type 24
